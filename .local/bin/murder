#!/usr/bin/env perl

use strict;
use warnings;
use feature 'say';
use Time::HiRes qw(sleep);

our @SIGNALS = (
    [15, 3],
    [2, 3],
    [1, 4],
    [9, 0]
);

sub go_ahead {
    my $input = <STDIN>;
    chomp($input);
    $input =~ s/^\s+|\s+$//g;
    $input = lc($input);

    return $input =~ /^(y|yes|yas)$/ ? 1 : 0;
}

sub is_running {
    my ($pid) = @_;
    return `ps -p $pid` =~ tr/\n// == 2;
}

sub murder_pid {
    my ($pid) = @_;
    foreach my $sig (@SIGNALS) {
        my ($code, $wait) = @$sig;
        kill($code, $pid);
        sleep(0.5);
        return unless is_running($pid);
        sleep($wait);
    }
}

sub murder_names {
    my ($name) = @_;
    my $running = `ps -eo 'pid command' | grep -Fiw '$name' | grep -Fv grep`;
    foreach my $line (split(/\n/, $running)) {
        my ($pid, $fullname) = split(/\s+/, $line, 2);
        next if $$ == $pid;
        print("Kill process '$fullname' (pid $pid)? [y/N] ");
        if (go_ahead()) {
            murder_pid($pid);
        }
    }
}

sub murder {
    my ($arg) = @_;
    my $is_pid = $arg =~ /^\d+$/;

    if ($is_pid) {
        murder_pid($arg);
    } else {
        murder_names($arg);
    }
}

if (@ARGV < 1) {
    say 'usage:';
    say 'murder 123    # kill by pid';
    say 'murder ruby   # kill by process name';
} else {
    foreach my $arg (@ARGV) {
        murder($arg);
    }
}
