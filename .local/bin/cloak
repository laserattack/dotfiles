#!/usr/bin/env perl

use strict;
use warnings;
use File::Basename;
use feature 'say';

our $encext = "1937";

my $PURPLE = "\033[35m";
my $RESET = "\033[0m";

sub purple {
    my ($text) = @_;
    return $PURPLE . $text . $RESET;
}

sub goahead {
    my $input = <STDIN>;
    chomp($input);
    $input =~ s/^\s+|\s+$//g;
    $input = lc($input);
    return $input =~ /^(y|yes|yas)$/ ? 1 : 0;
}

sub cloakdir {
    my ($path) = @_;
    my $dirname = basename($path);
    my $parent = dirname($path);
    my $output = "$dirname.tar.gz.$encext";

    print "encrypt dir " . purple("$path") . " -> " . purple($output) . "? [y/N] ";
    return unless goahead();

    `tar czf - -C '$parent' '$dirname' | openssl enc -aes-256-cbc -salt -pbkdf2 -out '$output'`;
    die "dir encrypt error" unless $? == 0;
}

sub cloakfile {
    my ($path) = @_;
    
    my $basename = basename($path);
    my $output;
    my $is_encrypted = $path =~ /\.$encext$/;
    
    if ($is_encrypted) {
        $output = $path;
        $output =~ s/\.$encext$//;
        
        print "decrypt file " . purple("$path") . " -> " . purple($output) . "? [y/N] ";
        return unless goahead();

        `openssl enc -aes-256-cbc -d -salt -pbkdf2 -in '$path' -out '$output'`;
        die "file decrypt error" unless $? == 0;

        if ($output =~ /\.tar\.gz$/) {
            print "unzip archive " . purple("$output") . "? [y/N] ";
            return unless goahead();

            `tar xzf '$output' -C .`;
            die "unzip archive error" unless $? == 0;
            unlink $output;
        }
    } else {
        $output = "$basename.$encext";
        print "encrypt file " . purple("$path") . " -> " . purple($output) . "? [y/N] ";
        return unless goahead();

        `openssl enc -aes-256-cbc -salt -pbkdf2 -in '$path' -out '$output'`;
        die "encrypt file error" unless $? == 0;
    }
}

sub cloak {
    my ($arg) = @_;
    if (-f $arg) {
        cloakfile($arg);
    } elsif (-d $arg) {
        cloakdir($arg);
    } else {
        die "'$arg' is not a file or dir\n";
    }
}

if (@ARGV < 1) {
    say 'cloak <porno path>';
} else {
    foreach my $arg (@ARGV) {
        cloak($arg);
    }
    say 'everything all right. bye bye!'
}
